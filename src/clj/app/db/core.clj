(ns app.db.core
  (:require
    [cheshire.core :refer [generate-string parse-string]]
    [next.jdbc.date-time]
    [next.jdbc.prepare]
    [next.jdbc :as jdbc]
    [next.jdbc.sql :as sql]
    [next.jdbc.result-set :as rs]
    [honeysql.core :as hsql]
    [clojure.set :refer [rename-keys]]
    [hikari-cp.core :refer [make-datasource]]
    [to-jdbc-uri.core :refer [to-jdbc-uri]]
    [clojure.tools.logging :as log]
    [app.config :refer [env]]
    [mount.core :refer [defstate]])
  (:import (org.postgresql.util PGobject)))

;; copied from conman
(defn- format-url [pool-spec]
  (if (:jdbc-url pool-spec)
    (update pool-spec :jdbc-url to-jdbc-uri)
    pool-spec))

(defn make-config [{:keys [jdbc-url adapter datasource datasource-classname] :as pool-spec}]
  (when (not (or jdbc-url adapter datasource datasource-classname))
    (throw (Exception. "one of :jdbc-url, :adapter, :datasource, or :datasource-classname is required to initialize the connection!")))
  (-> pool-spec
      (format-url)
      (rename-keys
        {:auto-commit?  :auto-commit
         :conn-timeout  :connection-timeout
         :min-idle      :minimum-idle
         :max-pool-size :maximum-pool-size})))

(defn connect!
  "attempts to create a new connection and set it as the value of the conn atom,
   does nothing if conn atom is already populated"
  [pool-spec]
  (make-datasource (make-config pool-spec)))

(defn disconnect!
  "checks if there's a connection and closes it
   resets the conn to nil"
  [conn]
  (when-let [ds (:datasource conn)]
    (when-not (.isClosed ds)
      (.close ds))))

(defn reconnect!
  "calls disconnect! to ensure the connection is closed
   then calls connect! to establish a new connection"
  [conn pool-spec]
  (disconnect! conn)
  (connect! pool-spec))
;; copied from conman

(defstate ^:dynamic conn
  :start (if-let [jdbc-url (env :database-url)]
           (connect! {:jdbc-url jdbc-url})
           (do
             (log/warn "database connection URL was not found, please set :database-url in your config, e.g: dev-config.edn")
             conn))
  :stop (disconnect! conn))

(defn pgobj->clj [^org.postgresql.util.PGobject pgobj]
  (let [type (.getType pgobj)
        value (.getValue pgobj)]
    (case type
      "json" (parse-string value true)
      "jsonb" (parse-string value true)
      "citext" (str value)
      value)))

(extend-protocol next.jdbc.result-set/ReadableColumn
  java.sql.Timestamp
  (read-column-by-label [^java.sql.Timestamp v _]
    (.toLocalDateTime v))
  (read-column-by-index [^java.sql.Timestamp v _2 _3]
    (.toLocalDateTime v))
  java.sql.Date
  (read-column-by-label [^java.sql.Date v _]
    (.toLocalDate v))
  (read-column-by-index [^java.sql.Date v _2 _3]
    (.toLocalDate v))
  java.sql.Time
  (read-column-by-label [^java.sql.Time v _]
    (.toLocalTime v))
  (read-column-by-index [^java.sql.Time v _2 _3]
    (.toLocalTime v))
  java.sql.Array
  (read-column-by-label [^java.sql.Array v _]
    (vec (.getArray v)))
  (read-column-by-index [^java.sql.Array v _2 _3]
    (vec (.getArray v)))
  org.postgresql.util.PGobject
  (read-column-by-label [^org.postgresql.util.PGobject pgobj _]
    (pgobj->clj pgobj))
  (read-column-by-index [^org.postgresql.util.PGobject pgobj _2 _3]
    (pgobj->clj pgobj)))

(defn clj->jsonb-pgobj [value]
  (doto (PGobject.)
    (.setType "jsonb")
    (.setValue (generate-string value))))

(extend-protocol next.jdbc.prepare/SettableParameter
  clojure.lang.IPersistentMap
  (set-parameter [^clojure.lang.IPersistentMap v ^java.sql.PreparedStatement stmt ^long idx]
    (.setObject stmt idx (clj->jsonb-pgobj v)))
  clojure.lang.IPersistentVector
  (set-parameter [^clojure.lang.IPersistentVector v ^java.sql.PreparedStatement stmt ^long idx]
    (let [conn      (.getConnection stmt)
          meta      (.getParameterMetaData stmt)
          type-name (.getParameterTypeName meta idx)]
      (if-let [elem-type (when (= (first type-name) \_)
                           (apply str (rest type-name)))]
        (.setObject stmt idx (.createArrayOf conn elem-type (to-array v)))
        (.setObject stmt idx (clj->jsonb-pgobj v))))))

;;  --------------------------------------
(defn update! [t w s]
  (sql/update! conn t w s
    {:builder-fn rs/as-unqualified-lower-maps}))

(defn get-by-id
  ([t id]
   (sql/get-by-id conn t id
     {:builder-fn rs/as-unqualified-lower-maps}))
  ([t id ex]
   (sql/get-by-id conn t id
     (merge
       {:builder-fn rs/as-unqualified-lower-maps}
       ex))))

(defn find-by-keys
  ([t w]
   (sql/find-by-keys conn t w
     {:builder-fn rs/as-unqualified-lower-maps}))
  ([t w ex]
   (sql/find-by-keys conn t w
     (merge
       {:builder-fn rs/as-unqualified-lower-maps}
       ex))))

(defn find-one-by-keys [t w]
  (first
    (sql/find-by-keys conn t w
      {:builder-fn rs/as-unqualified-lower-maps})))

(defn insert! [t info]
  (sql/insert! conn t info
    {:builder-fn rs/as-unqualified-lower-maps}))

(defn delete! [t w]
  (sql/delete! conn t w))

(defn execute!
  ([sqlmap]
   (jdbc/execute! conn sqlmap
     {:builder-fn rs/as-unqualified-lower-maps}))
  ([sqlmap opt]
   (jdbc/execute! conn sqlmap
     (merge
       {:builder-fn rs/as-unqualified-lower-maps}
       opt))))
